import {
  core.dictionary,
  core.float.show {show-float},
  core.int.show {show-int},
  core.list {append, for-each, intercalate},
  core.list.show,
  core.show {Show, show},
  core.text {join},
  core.text.show,
  this.data {Array, Bool, Float, Integer, Null, Object, Text, json, object},
  this.text.show {_show-json-text},
}

nominal {
  define show-json(j: json): text,
}

inline as-show: show(json) {
  Show{show := show-json}
}

inline list-json: show(list(json)) {
  core.list.show.as-show(as-show)
}

inline showable-object: show(object(json)) {
  Show{
    show := {
      function (kvs) {
        let kvs = core.dictionary.to-list(kvs);
        let kvs =
          for-each(kvs, function (kv) {
            let Pair(k, v) = kv;
            let k = _show-json-text(k);
            let v = show-json(v);
            join(List[k, *": ", v])
          });
        join(Cons(*"{", append(intercalate(kvs, *", "), List[*"}"])))
      }
    },
  }
}

define _show-object(kvs: object(json)): list(text) {
  let kvs = core.dictionary.to-list(kvs);
  let kvs =
    for-each(kvs, function (kv) {
      let Pair(k, v) = kv;
      let k = {
        let Show{show} = core.text.show.as-show;
        show(k)
      };
      let v = show-json(v);
      join(List[k, *": ", v])
    });
  Cons(*"{", append(intercalate(kvs, *", "), List[*"}"]))
}

define show-json(j: json): text {
  match j {
  | Null =>
    *"null"
  | Bool(b) =>
    if b {
      *"true"
    } else {
      *"false"
    }
  | Integer(i) =>
    show-int(i)
  | Float(x) =>
    show-float(x, 6)
  | Text(x) =>
    _show-json-text(x)
  | Object(kvs) =>
    let Show{show} = showable-object;
    show(kvs)
  | Array(xs) =>
    let Show{show} = list-json;
    show(xs)
  }
}
