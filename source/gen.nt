import {
  core.dictionary {from-list, to-list},
  core.list {for-each},
  core.random {random-in-range-int},
  noa.move.gen.bool {bool-gen},
  noa.move.gen.float {float-gen},
  noa.move.gen.int {int-gen},
  noa.move.gen.list {list-gen},
  noa.move.gen.pair {pair-gen},
  noa.move.gen.text {text-gen},
  noa.rule.gen {Gen, gen},
  this.data {Array, Bool, Float, Integer, Null, Object, Text, json},
  this.show {as-show},
}

nominal {
  json-gen: gen(json),
}

define json-list-gen(): gen(list(json)) {
  list-gen(json-gen)
}

define kvs-list-gen(): gen(list(pair(text, json))) {
  list-gen(pair-gen(text-gen, json-gen))
}

inline json-gen: gen(json) {
  Gen of {
    generate = {
      function (size: int) {
        let dice = random-in-range-int(0, 6) in
        if eq-int(dice, 0) {
          Null
        } else-if eq-int(dice, 1) {
          let Gen of {generate} = bool-gen in
          Bool(generate(size))
        } else-if eq-int(dice, 2) {
          let Gen of {generate} = int-gen in
          Integer(generate(size))
        } else-if eq-int(dice, 3) {
          let Gen of {generate} = float-gen in
          Float(generate(size))
        } else-if eq-int(dice, 4) {
          let Gen of {generate} = text-gen in
          Text(generate(size))
        } else-if eq-int(dice, 5) {
          let size = div-int(size, 2) in
          let Gen of {generate} = kvs-list-gen() in
          Object(from-list(core.text.ord.as-ord, generate(size)))
        } else {
          let size = div-int(size, 2) in
          let Gen of {generate} = json-list-gen() in
          Array(generate(size))
        }
      }
    },
    shrink = {
      function (j: json) {
        match j {
        | Null =>
          []
        | Bool(b) =>
          let Gen of {shrink} = bool-gen in
          for-each(shrink(b), Bool)
        | Integer(x) =>
          let Gen of {shrink} = int-gen in
          for-each(shrink(x), Integer)
        | Float(x) =>
          let Gen of {shrink} = float-gen in
          for-each(shrink(x), Float)
        | Text(x) =>
          let Gen of {shrink} = text-gen in
          for-each(shrink(x), Text)
        | Object(kvs) =>
          let Gen of {shrink} = kvs-list-gen() in
          for-each(shrink(to-list(kvs)), function (kvs) {
            Object(from-list(core.text.ord.as-ord, kvs))
          })
        | Array(xs) =>
          let Gen of {shrink} = json-list-gen() in
          for-each(shrink(xs), Array)
        }
      }
    },
    viewer = as-show,
  }
}
