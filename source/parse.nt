import {
  core.dictionary,
  core.float.parse {parse-float},
  core.function {compose},
  core.int.parse {parse-int},
  core.list {any, reverse},
  core.text {join, singleton},
  this.data {Array, Bool, Float, Integer, Null, Object, Text, insert, json, object},
  this.text.parse {_parse-json-text},
  zonk.move.make-parse-error {make-parse-error},
  zonk.move.parse {
    any-rune,
    branch,
    chunk,
    drop-while,
    end-of-input,
    optional,
    parsed,
    take-while,
    zonk,
  },
  zonk.rule.error {parse-error},
  zonk.rule.expected-item {_Label},
  zonk.rule.zonk-kit {zonk-kit},
}

define _is-space(r: rune): bool {
  if eq-rune(r, ` `) {
    True
  } else-if ule-int32(sub-int32(magic cast(rune, int32, r), 0x9), 0x4) {
    True
  } else {
    False
  }
}

inline _skip-space: zonk(unit) {
  drop-while(_is-space)
}

inline _token(t: &text): zonk(unit) {
  function (k) {
    try _ = chunk(t)(k) in
    try _ = _skip-space(k) in
    Right(Unit)
  }
}

inline _parse-null: zonk(json) {
  function (k) {
    try _ = _token("null")(k) in
    Right(Null)
  }
}

inline _parse-bool: zonk(json) {
  branch(
    function (k) {
      try _ = _token("true")(k) in
      Right(Bool(True))
    },
    function (k) {
      try _ = _token("false")(k) in
      Right(Bool(False))
    },
  )
}

define _any-number(r: rune): bool {
  let b1 = le-rune(`0`, r) in
  let b2 = le-rune(r, `9`) in
  and(b1, b2)
}

define _is-integer-rune(r: rune): bool {
  or(
    _any-number(r),
    or(
      eq-rune(r, `+`),
      eq-rune(r, `-`),
    ),
  )
}

define _is-float-rune(r: rune): bool {
  or(
    _any-number(r),
    or(
      eq-rune(r, `.`),
      or(
        eq-rune(r, `e`),
        or(
          eq-rune(r, `E`),
          or(
            eq-rune(r, `+`),
            eq-rune(r, `-`),
          ),
        ),
      ),
    ),
  )
}

inline _parse-number: zonk(json) {
  // RFC8259:
  // > This specification allows implementations to set limits on the range
  // > and precision of floats accepted.
  function (k) {
    try num-text = take-while(_is-float-rune)(k) in
    try _ = _skip-space(k) in
    let is-integer on num-text = core.text.all(num-text, _is-integer-rune) in
    if is-integer {
      let num-or-none on num-text = parse-int(num-text) in
      let _ = num-text in
      match num-or-none {
      | Right(num) =>
        Right(Integer(num))
      | Left(_) =>
        Left(_Label("number"))
        // report-unexpected-input("number")
      }
    } else {
      let num-or-none on num-text = parse-float(num-text) in
      let _ = num-text in
      match num-or-none {
      | Right(num) =>
        Right(Float(num))
      | Left(_) =>
        Left(_Label("number"))
        // report-unexpected-input("number")
      }
    }
  }
}

define _special-text(r: rune): bool {
  let b1 = eq-rune(r, `"`) in
  let b2 = eq-rune(r, `\\`) in
  or(b1, b2)
}

define _parse-text-loop(acc: list(text)): zonk(text) {
  function (k) {
    try piece = take-while(compose(not, _special-text))(k) in
    try backslash-or-none = optional(chunk("\\"))(k) in
    match backslash-or-none {
    | Right(_) =>
      try r = any-rune(k) in
      _parse-text-loop(Cons(singleton(r), Cons(*"\\", Cons(piece, acc))))(k)
    | Left(_) =>
      try _ = _token("\"")(k) in
      Right(join(core.list.reverse(Cons(piece, acc))))
    }
  }
}

inline _parse-text-inner(): zonk(text) {
  function (k) {
    try _ = chunk("\"")(k) in
    try t = _parse-text-loop([])(k) in
    Right(t)
  }
}

inline _parse-text: zonk(json) {
  function (k) {
    try t = _parse-text-inner()(k) in
    let unquoted-text-or-none on t = _parse-json-text(t) in
    let _ = t in
    match unquoted-text-or-none {
    | Right(t') =>
      Right(Text(t'))
    | Left(_) =>
      Left(_Label("JSON string"))
      // report-unexpected-input("JSON string")
    }
  }
}

nominal {
  _parse-json: zonk(json),
}

inline _parse-object-item(kvs: object(json)): zonk(object(json)) {
  function (k) {
    try key = _parse-text-inner()(k) in
    try _ = _token(":")(k) in
    try value = _parse-json(k) in
    let unquoted-key-or-none on key = _parse-json-text(key) in
    let _ = key in
    match unquoted-key-or-none {
    | Right(key') =>
      Right(insert(kvs, key', value))
    | Left(_) =>
      Left(_Label("JSON key"))
      // report-unexpected-input("JSON key")
    }
  }
}

define _parse-object-inner(k: &zonk-kit, acc: object(json)): parsed(json) {
  try acc' = _parse-object-item(acc)(k) in
  try comma-or-none = optional(_token(","))(k) in
  match comma-or-none {
  | Right(_) =>
    _parse-object-inner(k, acc')
  | Left(_) =>
    Right(Object(acc'))
  }
}

inline _parse-object: zonk(json) {
  function (k) {
    try _ = _token("{")(k) in
    try kvs =
      branch(
        function (k) {
          _parse-object-inner(k, core.dictionary.empty)
        },
        function (_) {
          Right(Object(core.dictionary.empty))
        },
      )(k)
    in
    try _ = _token("}")(k) in
    Right(kvs)
  }
}

define _parse-array-inner(k: &zonk-kit, acc: list(json)): parsed(json) {
  try v = _parse-json(k) in
  try comma-or-none = optional(_token(","))(k) in
  match comma-or-none {
  | Right(_) =>
    _parse-array-inner(k, Cons(v, acc))
  | Left(_) =>
    Right(Array(reverse(Cons(v, acc))))
  }
}

inline _parse-array: zonk(json) {
  function (k) {
    try _ = _token("[")(k) in
    try vs =
      branch(
        function (k) {
          _parse-array-inner(k, [])
        },
        function (_) {
          Right(Array([]))
        },
      )(k)
    in
    try _ = _token("]")(k) in
    Right(vs)
  }
}

inline _parse-json: zonk(json) {
  branch(
    _parse-null,
    branch(
      _parse-bool,
      branch(
        _parse-object,
        branch(
          _parse-text,
          branch(
            _parse-number,
            _parse-array,
          ),
        ),
      ),
    ),
  )
}

define parse-json(k: &zonk-kit): either(parse-error, json) {
  let p: zonk(json) =
    function (k) {
      try _ = _skip-space(k) in
      try json = _parse-json(k) in
      try _ = end-of-input(k) in
      Right(json)
    }
  in
  match p(k) {
  | Right(v) =>
    Right(v)
  | Left(e) =>
    Left(make-parse-error(k, e))
  }
}
