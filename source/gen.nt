import {
  core.dictionary {from-list, to-list},
  core.random {random-in-range},
  noa.gen {Gen, gen},
  noa.gen.bools {bools},
  noa.gen.floats {floats},
  noa.gen.ints {ints},
  noa.gen.lists {lists},
  noa.gen.pairs {pairs},
  noa.gen.texts {texts},
  this.data {Array, Bool, Float, Integer, Null, Object, Text, json},
  this.show {as-show},
}

nominal {
  jsons: gen(json),
}

define json-lists(): gen(list(json)) {
  lists(jsons)
}

define kvs-lists(): gen(list(pair(text, json))) {
  lists(pairs(texts, jsons))
}

constant jsons: gen(json) {
  Gen of {
    generate = {
      function (size: int) {
        let dice = random-in-range(0, 6) in
        if eq-int(dice, 0) {
          Null
        } else-if eq-int(dice, 1) {
          let Gen of {generate} = bools in
          Bool(generate(size))
        } else-if eq-int(dice, 2) {
          let Gen of {generate} = ints in
          Integer(generate(size))
        } else-if eq-int(dice, 3) {
          let Gen of {generate} = floats in
          Float(generate(size))
        } else-if eq-int(dice, 4) {
          let Gen of {generate} = texts in
          Text(generate(size))
        } else-if eq-int(dice, 5) {
          let size = div-int(size, 2) in
          let Gen of {generate} = kvs-lists() in
          Object(from-list(core.text.ord.as-ord, generate(size)))
        } else {
          let size = div-int(size, 2) in
          let Gen of {generate} = json-lists() in
          Array(generate(size))
        }
      }
    },
    shrink = {
      function (j: json) {
        match j {
        | Null =>
          []
        | Bool(b) =>
          let Gen of {shrink} = bools in
          for-each(shrink(b), Bool)
        | Integer(x) =>
          let Gen of {shrink} = ints in
          for-each(shrink(x), Integer)
        | Float(x) =>
          let Gen of {shrink} = floats in
          for-each(shrink(x), Float)
        | Text(x) =>
          let Gen of {shrink} = texts in
          for-each(shrink(x), Text)
        | Object(kvs) =>
          let Gen of {shrink} = kvs-lists() in
          for-each(shrink(to-list(kvs)), function (kvs) {
            Object(from-list(core.text.ord.as-ord, kvs))
          })
        | Array(xs) =>
          let Gen of {shrink} = json-lists() in
          for-each(shrink(xs), Array)
        }
      }
    },
    viewer = as-show,
  }
}
