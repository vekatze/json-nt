import {
  noa.gen.texts {texts},
  noa.suite {Trope, noa},
  this.data {json},
  this.gen {jsons},
  this.parse {parse-json},
  this.show {show-json},
  this.text.parse {_parse-json-text},
  this.text.show {_show-json-text-without-quote},
  zonk.move.make-zonk-kit {make-zonk-kit},
}

define main(): unit {
  let Trope of {check = !check} = noa in
  !check(
    "∀ (t: text). quote(parse(quote(t))) == quote(t)",
    texts,
    function (t: text) {
      pin t = t in
      pin t' = _show-json-text-without-quote(t) in
      match _parse-json-text(t') {
      | Left(_) =>
        False
      | Right(t'') =>
        pin t'' = t'' in
        pin t3 = _show-json-text-without-quote(t'') in
        eq-text(t', t3)
      }
    },
  );
  !check(
    "∀ (j: json). show(parse(show(j))) == show(j)",
    jsons,
    function (j: json) {
      pin json-txt = show-json(j) in
      pin k = make-zonk-kit(*json-txt) in
      match parse-json(k) {
      | Right(j) =>
        pin new-json-txt = show-json(j) in
        eq-text(json-txt, new-json-txt)
      | Left(_) =>
        False
      }
    },
  )
}

define zen(): unit {
  pin k = make-zonk-kit(*" {\"key\" : 1234}") in
  match parse-json(k) {
  | Right(j) =>
    printf("ok: {}\n", [show-json(j)])
  | Left(_) =>
    print("unreachable")
  }
}
