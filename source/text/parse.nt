import {
  core.int.parse {parse-hex},
  core.int.show {show-int},
  core.list {any, concat, for-each-E, for-each-EN, intercalate},
  core.rune,
  core.text {drop, format, singleton, split, take-N},
  core.text.io {print-line},
}

data error {
| JSON-String-Error(text)
}

define _is-hex-digit(r: rune): bool {
  any(
    List[
      and(le-rune(`0`, r), le-rune(r, `9`)),
      and(le-rune(`a`, r), le-rune(r, `f`)),
      and(le-rune(`A`, r), le-rune(r, `F`)),
    ],
  )
}

define _unquote-text(t: text): either(error, list(text)) {
  match core.text.uncons(t) {
  | Left(_) =>
    Right(List[*"\\"])
  | Right(Pair(c, rest)) =>
    if eq-rune(c, `b`) {
      Right(List[*"\u{8}", rest])
    } else-if eq-rune(c, `t`) {
      Right(List[*"\u{9}", rest])
    } else-if eq-rune(c, `n`) {
      Right(List[*"\u{a}", rest])
    } else-if eq-rune(c, `f`) {
      Right(List[*"\u{c}", rest])
    } else-if eq-rune(c, `r`) {
      Right(List[*"\u{d}", rest])
    } else-if eq-rune(c, `"`) {
      Right(List[*"\u{22}", rest])
    } else-if eq-rune(c, `/`) {
      Right(List[*"\u{2f}", rest])
    } else-if eq-rune(c, `\\`) {
      Right(List[*"\u{5c}", rest])
    } else-if eq-rune(c, `u`) {
      let scalar-value-text on rest = take-N(rest, 4);
      let len on scalar-value-text = core.text.length(scalar-value-text);
      pin scalar-value-text = scalar-value-text;
      if and(eq-int(len, 4), core.text.all(scalar-value-text, _is-hex-digit)) {
        match parse-hex(scalar-value-text) {
        | Left(_) =>
          Left(JSON-String-Error(format("Could not parse `{}` as a hexadecimal integer", List[*scalar-value-text])))
        | Right(v) =>
          match core.rune.from-int(v) {
          | Left(_) =>
            Left(JSON-String-Error(format("The value `{}` is outsidethe Unicode codespace", List[show-int(v)])))
          | Right(r) =>
            let rest = drop(rest, 4);
            Right(List[singleton(r), rest])
          }
        }
      } else {
        Left(JSON-String-Error(format("Expected 4-digit Unicode codepoint, but got: {}", List[*scalar-value-text])))
      }
    } else {
      Left(JSON-String-Error(format("Unknown escape sequence: \\{}", List[singleton(c)])))
    }
  }
}

define _parse-json-text-fragment(t: &text): either(error, list(text)) {
  match split(t, "\\") {
  | Nil =>
    // unreachable
    Left(JSON-String-Error(*"json.text.parse._parse-json-text-fragment"))
  | Cons(t, ts) =>
    try fragment-lists = for-each-E(ts, _unquote-text);
    Right(Cons(t, concat(fragment-lists)))
  }
}

define _parse-json-text(t: &text): either(error, text) {
  pin ts = split(t, "\\\\");
  try tss = for-each-EN(ts, _parse-json-text-fragment);
  Right(core.text.join(core.list.concat(intercalate(tss, List[*"\\"]))))
}

define zen(): unit {
  match _parse-json-text("hello,\\u12abrld") {
  | Left(JSON-String-Error(e)) =>
    pin e = e;
    print("error: ");
    print-line(e);
  | Right(t) =>
    pin t = t;
    print("success: ");
    print-line(t);
  }
}
