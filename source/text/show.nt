import {
  core.c-int {c-int, from-c-int},
  core.c-size {C-Size},
  core.c-string {c-string, to-c-string},
  core.external {malloc},
  core.list {append},
  core.memory {store-int},
  core.pointer {null-pointer, shift},
  core.rune,
  core.text {_get-rune, _slice-N, format, join, text-byte-length},
  core.word,
}

foreign {
  snprintf(pointer, c-int, pointer): c-int,
  sprintf(pointer, pointer): c-int,
}

define _show-hex(x: int32): text {
  pin fmt = to-c-string("%02x");
  let fmt = magic cast(&c-string, pointer, fmt);
  let size = magic external snprintf(null-pointer, 0, fmt)(x: int32);
  let size = from-c-int(size);
  let ptr = malloc(C-Size(add-int(size, add-int(1, mul-int(core.word.size, 2)))));
  store-int(0, ptr);
  store-int(size, shift(ptr, core.word.size));
  let buffer = shift(ptr, mul-int(core.word.size, 2));
  let _ = magic external sprintf(buffer, fmt)(x: int32);
  magic cast(pointer, text, ptr)
}

define _quote-text(haystack: &text): list(text) {
  let len = text-byte-length(haystack);
  let f =
    define !loop(pivot: int, cursor: int): list(text) {
      let index = add-int(pivot, cursor);
      if ge-int(index, len) {
        [_slice-N(haystack, pivot, cursor)]
      } else {
        let f =
          function (escaped-text: text) {
            let piece = _slice-N(haystack, pivot, cursor);
            let rest = !loop(add-int(index, 1), 0);
            Cons(piece, Cons(escaped-text, rest))
          };
        let r = _get-rune(haystack, index);
        match r {
        | `\u{8}` =>
          f(*"\\b")
        | `\u{9}` =>
          f(*"\\t")
        | `\u{a}` =>
          f(*"\\n")
        | `\u{c}` =>
          f(*"\\f")
        | `\u{d}` =>
          f(*"\\r")
        | `\u{22}` =>
          f(*"\\\"")
        | `\u{5c}` =>
          f(*"\\\\")
        | _ =>
          if le-rune(r, `\u{1f}`) {
            f(format("\\u00{}", [_show-hex(magic cast(rune, int32, r))]))
          } else {
            !loop(pivot, add-int(cursor, core.rune.size(r)))
          }
        }
      }
    };
  f(0, 0)
}

define _show-json-text(t: text): text {
  pin t = t;
  join(Cons(*"\"", append(_quote-text(t), [*"\""])))
}

define _show-json-text-without-quote(t: &text): text {
  join(_quote-text(t))
}
