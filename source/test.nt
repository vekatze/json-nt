import {
  core.text.io {print-line},
  noa.check {check},
  noa.gen.text {text-gen},
  noa.make-noa-kit {make-default-noa-kit},
  this.data {json},
  this.gen {json-gen},
  this.parse {parse-json},
  this.show {show-json},
  this.text.parse {_parse-json-text},
  this.text.show {_show-json-text-without-quote},
  zonk.make-zonk-kit {make-zonk-kit},
}

define main(): unit {
  pin k = make-default-noa-kit();
  check::(
    k,
    "∀ (t: text). quote(parse(quote(t))) == quote(t)",
    text-gen,
    function (t: text) {
      pin t = t;
      pin t-without-quote = _show-json-text-without-quote(t);
      match _parse-json-text(t-without-quote) {
      | Left(_) =>
        False
      | Right(t2) =>
        pin t2 = t2;
        pin t3 = _show-json-text-without-quote(t2);
        eq-text(t-without-quote, t3)
      }
    },
  );
  check::(
    k,
    "∀ (j: json). show(parse(show(j))) == show(j)",
    json-gen,
    function (j: json) {
      pin json-txt = show-json(j);
      pin k = make-zonk-kit(*json-txt, Unit);
      match parse-json(k) {
      | Right(j) =>
        pin new-json-txt = show-json(j);
        eq-data::(json-txt, new-json-txt)
      | Left(_) =>
        False
      }
    },
  )
}

define zen(): unit {
  pin k = make-zonk-kit(*" {\"key\" : 1234}", Unit);
  match parse-json(k) {
  | Right(j) =>
    pin j = show-json(j);
    print("ok: ");
    print-line(j);
  | Left(_) =>
    print("unreachable")
  }
}
